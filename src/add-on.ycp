/**
 * File:	clients/add-on.ycp
 * Package:	yast2-installation
 * Summary:	Install an add-on product
 * Authors:	Jiri Srain <jsrain@suse.de>
 *
 */

{
textdomain "add-on";

import "AddOnProduct";
import "Confirm";
import "PackageLock";
import "PackageCallbacksInit";
import "Report";
import "Wizard";
import "GetInstArgs";
import "Mode";
import "CommandLine";

include "add-on/add-on-workflow.ycp";

list wfm_args = WFM::Args();
y2milestone ("ARGS: %1", wfm_args);
if (
    (size (wfm_args) > 0)
    &&
    (contains (wfm_args, "help") || contains (wfm_args, "longhelp"))
) {
    Mode::SetUI ("commandline");
    // TRANSLATORS: commandline help
    CommandLine::Print(_("
Add-On Module Help
------------------

To add a new Add-On product via command-line, use this syntax:
    /sbin/yast2 add-on URL
URL is the path to the Add-On source.

Examples of URL:
http://server.name/directory/Lang-AddOn-10.2-i386/
ftp://server.name/directory/Lang-AddOn-10.2-i386/
nfs://server.name/directory/SDK1-SLE-i386/
disk://dev/sda5/directory/Product/CD1/
cd://
dvd://
"));
    return `auto;
}

Wizard::CreateDialog();

Wizard::SetContents (
    // dialog caption
    _("Add-On Product Media"),
    // busy message (dialog)
    `VBox(`Label(_("Initializing..."))),
    // help
    _("<p>Initializing add-on products...</p>"),
    false,
    false
);

Wizard::SetDesktopIcon("vendor");

Wizard::DisableBackButton();
Wizard::DisableAbortButton();
Wizard::DisableNextButton();

// --> Initialization start

// check whether running as root
// and having the packager for ourselves
if (! Confirm::MustBeRoot () || ! PackageLock::Check ())
{
    UI::CloseDialog ();
    return `abort;
}

// initialize target to import all trusted keys (#165849)
Pkg::TargetInit( "/", false );

PackageCallbacksInit::InitPackageCallbacks ();

// Initialize current sources
Read();
symbol ret = nil;

// <-- Initialization finish

Wizard::EnableAbortButton();
Wizard::EnableNextButton();

if (size (WFM::Args()) == 0)
{
    y2milestone ("Url not specified in the command line, asking user");
    ret = RunWizard();
}
else
{
    string url = (string)WFM::Args(0);
    y2milestone ("Specified URL %1", url);
    do {
	createResult = SourceManager::createSource (url);
	y2milestone ("Source creating result: %1", createResult);
    } while ( createResult == `again );
    AddOnProduct::last_ret = `next;
    ret = RunAutorunWizard ();
}

    // bugzilla #293428
    // Release all sources before adding a new one
    // because of CD/DVD + url cd://
    Pkg::SourceReleaseAll();

if (ret == `next)
{
    // feedback heading
    string heading = _("Add-On Product Installation");
    // feedback message
    string message
	= _("Reading packages available at the installation repositories...");

    Popup::ShowFeedback (heading, message);

    y2milestone ("syncing srcid %1 to zmd", AddOnProduct::src_id);
    boolean synced
	= SourceManager::SyncAddedAndDeleted ([AddOnProduct::src_id], []);
    y2milestone ("sync status: %1", synced);

    Popup::ClearFeedback ();

    WFM::CallFunction ("inst_addon_update_sources", []);
    ret = AddOnProduct::DoInstall ();
    // Write only when there are some changes
    Write();
}
UI::CloseDialog();
return ret;

/* EOF */
}
